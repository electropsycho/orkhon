plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id "org.beryx.jlink" version "2.22.1"
    id "io.freefair.lombok" version "5.2.1"

//    id 'org.javamodularity.moduleplugin' version '1.7.0' apply false
//    id  "com.gluonhq.client-gradle-plugin" version "0.1.33-SNAPSHOT"
}


group 'org.orkhon'
version '1.0.0'
compileJava.options.encoding = 'UTF-8'
repositories {
    mavenCentral()
    jcenter()
}

sourceCompatibility = "15"

dependencies {
    implementation 'org.jetbrains:annotations:19.0.0'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.0'

    implementation group: 'com.dlsc.afterburner', name: 'afterburner.fx', version: '2.1.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.0'
    implementation group: 'io.reactivex.rxjava3', name: 'rxjava', version: '3.0.6'
    implementation group: 'org.jdbi', name: 'jdbi3-core', version: '3.16.0'
    implementation group: 'org.jdbi', name: 'jdbi3-sqlobject', version: '3.16.0'
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '3.4.5'
    implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.7.0'


    compileOnly 'org.projectlombok:lombok:1.18.14'
    annotationProcessor 'org.projectlombok:lombok:1.18.14'

    testCompileOnly 'org.projectlombok:lombok:1.18.14'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.14'

    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:win"
}

javafx {
    version = "15"
    modules = [ 'javafx.base', 'javafx.controls', 'javafx.fxml' ]

}


mainClassName = 'org.orkhon.App'

application {
    mainModule = 'orkhon' // name defined in module-info.java
    mainClass = mainClassName
}

jar {
    manifest {
        attributes 'Main-Class' : mainClassName
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'orkhon'
        jvmArgs = ['-Djdk.gtk.version=2'] // required due to a bug in Java: https://github.com/javafxports/openjdk-jfx/issues/175
    }
    jpackage {
        if (javafx.getPlatform().name() == "OSX") {
            installerType = "dmg"
            jpackageHome = "/Users/<user>/Downloads/jdk-13.jdk/Contents/Home"
        } else if (javafx.getPlatform().name() == "WINDOWS") {
            jpackageHome = "C:\\Program Files\\AdoptOpenJDK\\jdk-15.0.0.36-openj9"
//            imageOptions += ['--icon', 'src/main/resources/images/united.ico']
            skipInstaller = true
            installerType = "exe"
            installerOptions += [
                    '--win-per-user-install', '--win-dir-chooser',
                    '--win-menu', '--win-shortcut',
                    '--verbose'
            ]
        }
    }
}

